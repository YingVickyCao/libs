apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version VERSION_NAME
group GROUP_NAME

def pomConfig = {
    licenses {
        license {
            name LICENSE
            url LICENSE_URL
            distribution LICENSE_distribution
        }
    }
    developers {
        developer {
            id DEVELOPER_ID
            name DEVELOPER_NAME
            email DEVELOPER_EMAIL
        }
    }

    scm {
        url SCM_URL
        connection SCM_CONNECTION
        developerConnection SCM_DEVELOPER_CONNECTION
    }
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/autils-release.aar")
            groupId GROUP_NAME
            artifactId ARTIFACT_ID
            version VERSION_NAME

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }

                def root = asNode()
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = "yingvickycao"
    key = "e71f17af38e8f63cae9c0ff4f3665d77d0e5dfb6"

    publications = ['Production']
    configurations = ['archives']

    publish = true
    dryRun = false
    override = true

    pkg {
        repo = REPO_NAME
        name = NAME

        userOrg = USER_ORG

        description = DESCRIPTION
        desc = DESCRIPTION

        publicDownloadNumbers = true

        licenses = ['Apache-2.0']
        labels = ['android', 'autils']

        vcsUrl = GIT_URL
        websiteUrl = SITE_URL
        issueTrackerUrl = ISSUE_TRACKER_URL

        version {
            name = this.version
            desc = DESCRIPTION
            released = new Date()
            vcsTag = this.version
        }
    }
}